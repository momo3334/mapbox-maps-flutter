buildscript {
    ext {
        agp_version = '8.3.0'
    }
}
allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    // Workaround for override compileSdkVersion
    // And force to use Java 17
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion 35

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }

        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_17.toString()
            }
        }

        // Workaround for add namespace for android library for using Gradle 8+
        if (project.plugins.hasPlugin('com.android.library')) {
            android {
                defaultConfig {
                    def androidManifest = android.sourceSets.main.manifest.srcFile
                    if (androidManifest.exists()) {
                        def parsedManifest = new XmlParser().parse(androidManifest)
                        def packageName = parsedManifest.attribute('package')

                        if (packageName) {
                            namespace = packageName
                        } else {
                            namespace = "com.mapbox.maps.mapbox_maps"
                        }
                    }
                }
            }
        }
    }
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
